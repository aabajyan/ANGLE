# Minimal version for standalone ANGLE build
macro(WEBKIT_INCLUDE_CONFIG_FILES_IF_EXISTS)
    set(_file ${CMAKE_CURRENT_SOURCE_DIR}/Platform${PORT}.cmake)
    if (EXISTS ${_file})
        message(STATUS "Using platform-specific CMakeLists: ${_file}")
        include(${_file})
    else ()
        message(STATUS "Platform-specific CMakeLists not found: ${_file}")
    endif ()
endmacro()

function(WEBKIT_COPY_FILES target_name)
    set(options FLATTENED)
    set(oneValueArgs DESTINATION)
    set(multiValueArgs FILES)
    cmake_parse_arguments(opt "${options}" "${oneValueArgs}" "${multiValueArgs}" ${ARGN})
    set(files ${opt_FILES})
    set(dst_files)
    # Ensure DESTINATION is not empty and not absolute root
    if (NOT opt_DESTINATION)
        set(opt_DESTINATION "${CMAKE_BINARY_DIR}/ANGLE/headers")
    endif ()
    foreach (file IN LISTS files)
        if (IS_ABSOLUTE ${file})
            set(src_file ${file})
        else ()
            set(src_file ${CMAKE_CURRENT_SOURCE_DIR}/${file})
        endif ()
        if (opt_FLATTENED)
            get_filename_component(filename ${file} NAME)
            set(dst_file ${opt_DESTINATION}/${filename})
        else ()
            get_filename_component(file_dir ${file} DIRECTORY)
            file(MAKE_DIRECTORY "${opt_DESTINATION}/${file_dir}")
            set(dst_file "${opt_DESTINATION}/${file}")
        endif ()
        add_custom_command(OUTPUT ${dst_file}
                COMMAND ${CMAKE_COMMAND} -E copy ${src_file} ${dst_file}
                DEPENDS ${src_file}
                COMMENT "Copying ${src_file} to ${dst_file}")
        list(APPEND dst_files ${dst_file})
    endforeach ()
    add_custom_target(${target_name} DEPENDS ${dst_files})
endfunction()
